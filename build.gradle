import javax.tools.ToolProvider

plugins {
    id 'net.researchgate.release' version '2.3.4' // release management
}

repositories {
    jcenter()
    flatDir {
        dirs 'lib'
    }
}

apply plugin: 'antlr'
apply plugin: 'java'
apply plugin: 'maven-publish'

group = "it.dexy.doclet"
description = "A custom Javadoc Doclet which generates JSON of all the Javadoc info"

ext {
    unsnapshottedVersion = version.replaceAll("-SNAPSHOT", "")
}

dependencies {
    antlr 'org.antlr:antlr:3.3'
    // antlr-3.3-complete.jar 	json_simple-1.1.jar 	sqlitejdbc-v056.jar
    // compile name: 'antlr-3.3-complete'
    compile name: 'json_simple-1.1'
    compile name: 'sqlitejdbc-v056'
    //    compile name: 'LaTeXlet'
    compile files(((URLClassLoader) ToolProvider.getSystemToolClassLoader()).getURLs())
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

jar {
    def libDir = "${projectDir}/lib/"
    from {
        configurations.compile.findAll {
            it.path.startsWith(libDir)
        }.collect {
            zipTree(it)
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
release {
    revertOnFail = true
    tagTemplate = "v${unsnapshottedVersion}"
}

publishing {
    repositories {
        maven {
            credentials {
                // You need to provide these property values somehow to be able to publish.
                // Putting them in ~/.gradle/gradle.properties works. See also
                // https://docs.gradle.org/current/userguide/build_environment.html
                username project.hasProperty('artifactoryTrdUsername') ? "${artifactoryTrdUsername}" : ""
                password project.hasProperty('artifactoryTrdPassword') ? "${artifactoryTrdPassword}" : ""
            }
            if (project.version.toString().endsWith('-SNAPSHOT')) {
                name 'ext-snapshot-local'
                url 'https://artifactory.trd.zedge.net/ext-snapshot-local'
            } else {
                name 'ext-release-local'
                url 'https://artifactory.trd.zedge.net/ext-release-local'
            }
        }
    }
    publications {
        jar(MavenPublication) {
            pom.withXml {
                // remove dependencies since we include them in the jar
                asNode().remove(asNode().dependencies)
            }
            from components.java
        }
        source(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

afterReleaseBuild.dependsOn publish
